subprojects {
  apply plugin: 'java'
  apply plugin: 'idea'
  apply plugin: 'maven'
  apply plugin: 'checkstyle'
  apply plugin: 'jacoco'
  apply plugin: 'findbugs'
  apply plugin: 'pmd'

  repositories {
      jcenter()
  }

  dependencies {
      // The production code uses the SLF4J logging API at compile time
      compile 'org.slf4j:slf4j-api:1.7.22'

      // Declare the dependency for your favourite test framework you want to use in your tests.
      // TestNG is also supported by the Gradle Test task. Just change the
      // testCompile dependency to testCompile 'org.testng:testng:6.8.1' and add
      // 'test.useTestNG()' to your build script.
      testCompile 'junit:junit:4.12'
  }

  checkstyle {
      toolVersion = "6.19"
      maxWarnings = 0
      configFile = new File(rootDir, "/config/checkstyle/checkstyle.xml")
  }

  jacoco {
      toolVersion = "0.7.9"
  }

  findbugs {
      toolVersion = "3.0.0"
  }

  tasks.withType(FindBugs) {
      reports {
          xml.enabled false
          html.enabled true
      }
  }

  pmd {
      toolVersion = "5.5.5"
  }

  jacocoTestCoverageVerification {
      dependsOn test
      violationRules {
          rule {
              element = 'BUNDLE'

              limit {
                  counter = 'BRANCH'
                  value = 'COVEREDRATIO'
                  minimum = 1.0
              }
          }
      }
  }

  check.dependsOn jacocoTestCoverageVerification
}

project(':test') {
    dependencies {
        compile project(':api')
        compile 'junit:junit:4.12' //This project is an extension of testing
    }
}

project(':lib') {
    dependencies {
        compile project(':api')
        testCompile 'uk.org.lidalia:slf4j-test:1.1.0'
    }
}
